MODULE PCRTM_ATM_ABSORPTION
  
  USE PCRTM_ATMOSPHERE_DEFINE
  USE PCRTM_JACOBIAN 
  USE PCRTM_ATM_ABSORPTION_DEFINE
  USE PCRTM_TYPE_KIND
  USE PCRTM_ATMOSPHERE_LAYER, ONLY : PCRTM_GEOMETRY_TYPE
  USE PCRTM_RT_SOLUTION_DEFINE
  USE PCRTM_MATH_UTILITY,     ONLY : PLANCK,DPLANCKDT,LOGXLOGP

 CONTAINS

   SUBROUTINE CALCODETC(ATM,                   &
                        PCRTM_ATM_ABS_COEF,    &
                        PCRTM_STND,            &
                        GEOMETRY,              &
                        RT_SOLUTION,           &
                        PCRTM_JACOB)
     TYPE(PCRTM_ATMOSPHERE_TYPE),  INTENT(INOUT)     ::  ATM
     TYPE(PCRTM_ATM_ABSORPTION_TYPE), INTENT(IN)     ::  PCRTM_ATM_ABS_COEF
     TYPE(PCRTM_ATM_ABS_STRUCT_TYPE), INTENT(IN)     ::  PCRTM_STND
     TYPE(PCRTM_NM_JACOBIAN_TYPE), INTENT(INOUT)     ::  PCRTM_JACOB
     TYPE(PCRTM_GEOMETRY_TYPE),       INTENT(IN)     ::  GEOMETRY
     TYPE(PCRTM_RT_SOLUTION_TYPE), INTENT(INOUT)     ::  RT_SOLUTION

     REAL(SINGLE) :: TAU     
     REAL(SINGLE) :: H2OVMR(PCRTM_STND%NLAY),H2OVMRSQ(PCRTM_STND%NLAY)
     REAL(SINGLE) :: WATERK1
     REAL(SINGLE) :: WATERK2
     REAL(SINGLE) :: A1
     REAL(SINGLE) :: A2
     REAL(SINGLE) :: AMTFIX
     REAL(SINGLE) :: DADT
     INTEGER      :: IG, IM, IT, I1, IGG
     
     RT_SOLUTION%TRTOP2LV(:,GEOMETRY%NTOP) = 1.0

!     CALCULATE MONO OPTICAL ABSORPTION FIRST:
     DO I1=GEOMETRY%NTOP,GEOMETRY%NBOT
        IF(ATM%TLAY(I1).GT.PCRTM_STND%TSTD(I1,PCRTM_STND%NT).OR.                                  &
           ATM%TLAY(I1).LT.PCRTM_STND%TSTD(I1,1))THEN
           PRINT*,'LAYER T IS OUTSIDE OF TABLE RANGE',I1
           PRINT*,ATM%TLAY(I1),PCRTM_STND%TSTD(I1,1),PCRTM_STND%TSTD(I1,PCRTM_STND%NT)
        ENDIF
        DO IT=2,PCRTM_STND%NT-1
           IF(ATM%TLAY(I1).LE.PCRTM_STND%TSTD(I1,IT)) EXIT
        END DO
        
        H2OVMR(I1)  =ATM%GASPROF(I1,1)/ATM%AIRAMT(I1)
        H2OVMRSQ(I1)= H2OVMR(I1)*H2OVMR(I1)
        
        DADT=1.0/(PCRTM_STND%TSTD(I1,IT)-PCRTM_STND%TSTD(I1,IT-1))
        A1=(ATM%TLAY(I1)-PCRTM_STND%TSTD(I1,IT-1))*DADT
        A2=1.0-A1
        
        AMTFIX=A1*PCRTM_STND%FIXAMT(IT,I1)+A2*PCRTM_STND%FIXAMT(IT-1,I1)
        ATM%GASPROF(I1,PCRTM_STND%NMOL+1)=(ATM%AIRAMT(I1))/AMTFIX
        
!     CALC. FIX GAS OD (THE DATABASE IS ALREADY IN THE UNIT OF OD):
        DO IM=1,PCRTM_STND%NM
           
           !     CALC. WATER OD:
           !     FIRST, 3-POINT WATER INTERPOLATION:
           WATERK1=PCRTM_ATM_ABS_COEF%KH2O1(IM,I1,IT)+PCRTM_ATM_ABS_COEF%KH2O2(IM,I1,IT)          &
                *H2OVMR(I1) + PCRTM_ATM_ABS_COEF%KH2O3(IM,I1,IT)*H2OVMRSQ(I1)

           WATERK2=PCRTM_ATM_ABS_COEF%KH2O1(IM,I1,IT-1)+PCRTM_ATM_ABS_COEF%KH2O2(IM,I1,IT-1)      &
                *H2OVMR(I1) + PCRTM_ATM_ABS_COEF%KH2O3(IM,I1,IT-1)*H2OVMRSQ(I1)

!     NOW, 2-POINT TEMPERATURE INTERPOLATION:
           TAU=(A1*WATERK1+A2*WATERK2)*ATM%GASPROF(I1,1)

           IF(PCRTM_JACOB%JACOB)THEN
              PCRTM_JACOB%DTAUDT(IM,I1)=(DADT*WATERK1-DADT*WATERK2)*ATM%GASPROF(I1,1)
              PCRTM_JACOB%DTAUDH2O(IM,I1)=A1*(PCRTM_ATM_ABS_COEF%KH2O1(IM,I1,IT)+                 &
                   2.0*PCRTM_ATM_ABS_COEF%KH2O2(IM,I1,IT)*H2OVMR(I1)+                             &
                   3.0*PCRTM_ATM_ABS_COEF%KH2O3(IM,I1,IT)*H2OVMRSQ(I1))+                          &
                   A2*(PCRTM_ATM_ABS_COEF%KH2O1(IM,I1,IT-1)+                                      &
                   2.0*PCRTM_ATM_ABS_COEF%KH2O2(IM,I1,IT-1)*H2OVMR(I1)+                           &
                   3.0*PCRTM_ATM_ABS_COEF%KH2O3(IM,I1,IT-1)*H2OVMRSQ(I1))
           ENDIF


!     NOW FOR THE REST OF GASES:
           DO IGG=1,PCRTM_STND%NGAS(IM)
              IG =PCRTM_STND%IDGAS(IGG,IM)
              TAU=TAU+(A1*PCRTM_ATM_ABS_COEF%KGAS(IGG,IM,I1,IT)                                   &
                   +A2*PCRTM_ATM_ABS_COEF%KGAS(IGG,IM,I1,IT-1))*ATM%GASPROF(I1,IG)

              IF(PCRTM_JACOB%JACOB)THEN
                 PCRTM_JACOB%DTAUDT(IM,I1)=PCRTM_JACOB%DTAUDT(IM,I1)+                             &
                      (DADT*PCRTM_ATM_ABS_COEF%KGAS(IGG,IM,I1,IT) &
                      -DADT*PCRTM_ATM_ABS_COEF%KGAS(IGG,IM,I1,IT-1))*ATM%GASPROF(I1,IG)
                 PCRTM_JACOB%DTAUDGAS(IG,IM,I1)=A1*PCRTM_ATM_ABS_COEF%KGAS(IGG,IM,I1,IT)          &
                      +A2*PCRTM_ATM_ABS_COEF%KGAS(IGG,IM,I1,IT-1)
              END IF

           ENDDO
           
           IF(TAU .LE.0)THEN
              TAU=0 
           END IF
           
           RT_SOLUTION%TRLAY(IM,I1)     = EXP(-TAU*GEOMETRY%SECZANG)+1e-10
           RT_SOLUTION%TAULAY(IM,I1)    = TAU
!!$           write(1111,*) RT_SOLUTION%TRLAY(IM,I1), TAU

        ENDDO                  !IM LOOP

        IF ( I1 .gt.1 ) then
           IF(ATM%CLD_FLAG(I1-1) .GT.0 ) THEN
              DO IM = 1, PCRTM_STND%nM
                 CALL LOGXLOGP(RT_SOLUTION%TAULAY(im,I1-1),RT_SOLUTION%TAULAY(IM,I1),PCRTM_STND%PBND(I1-1),&
                      PCRTM_STND%PBND(I1),ATM%PCLD(I1-1),TAU)
                 RT_SOLUTION%TAUGAS_ABOVE(im) = sum(RT_SOLUTION%TAUlay(IM,1:I1-2))+TAU
                 RT_SOLUTION%TAUGAS_BELOW(im) = sum(RT_SOLUTION%TAUlay(IM,I1-1:PCRTM_STND%NLAY))-TAU
!!$                 write(1112,*) RT_SOLUTION%Taulay(im,I1-1),RT_SOLUTION%Taulay(im,I1),PCRTM_STND%PBND(I1-1),PCRTM_STND%PBND(I1),ATM%PCLD(I1-1),Tau
              END DO
           END IF
        end IF

        RT_SOLUTION%TRTOP2LV(:,I1+1) = RT_SOLUTION%TRTOP2LV(:,I1)*RT_SOLUTION%TRLAY(:,I1) 
        RT_SOLUTION%BLAY(:,I1)   = PLANCK(PCRTM_STND%FRQ,ATM%TLAY(I1),PCRTM_STND%NM)
        RT_SOLUTION%DBDT(:,I1)   = DPLANCKDT(PCRTM_STND%FRQ,ATM%TLAY(I1),PCRTM_STND%NM)  
        RT_SOLUTION%BS           = PLANCK(PCRTM_STND%FRQ,ATM%TSKIN,PCRTM_STND%NM)
        RT_SOLUTION%DBSDTS       = DPLANCKDT(PCRTM_STND%FRQ,ATM%TSKIN,PCRTM_STND%NM)

     ENDDO                     !I1 LOOP

     RETURN
   END SUBROUTINE CALCODETC

 END MODULE PCRTM_ATM_ABSORPTION
