MODULE PCRTM_ATM_ABSORPTION_IO
  USE PCRTM_FILE_UTILITIES, ONLY  : GETLUN
  USE PCRTM_TYPE_KIND
  USE PCRTM_ATM_ABSORPTION_DEFINE


CONTAINS
  SUBROUTINE RD_PCRTM_ATM_ABSCOEF(PCRTM_ATM_ABS_COEF,ABSCOEFFILE,PCRTM_STND)
    TYPE(PCRTM_ATM_ABSORPTION_TYPE),INTENT(OUT) :: PCRTM_ATM_ABS_COEF 
    TYPE(PCRTM_ATM_ABS_STRUCT_TYPE),INTENT(OUT) :: PCRTM_STND
    CHARACTER(160),INTENT(IN)             :: ABSCOEFFILE

    INTEGER :: IUABS
    INTEGER :: NM, NT, NLAY, NMOL
    REAL(DOUBLE), ALLOCATABLE :: ABSCOEF(:,:,:,:)
    REAL(DOUBLE), ALLOCATABLE :: KFIX(:,:,:)
    INTEGER      :: ALLOC_STAT,DEALLOC_STAT
    INTEGER      :: IG, IM, I1, IT

    CALL GETLUN(IUABS)
    OPEN(UNIT=IUABS,FILE=ABSCOEFFILE,FORM='UNFORMATTED')
    READ(IUABS) NM, NT, NLAY, NMOL

    PRINT*, 'LOADING ABSORPTION COEFFICIENT DATA FROM ....',ABSCOEFFILE
    PRINT*, 'NUMBER OF MONO FREQUENCY BINS -',NM 
    PRINT*, 'NUMBER OF STANDARD ATMOSPHERIC PROFILES',NT
    PRINT*, 'NUMBER OF ATMOSPHERE LAYERS',NLAY
    PRINT*, 'NUMBER OF ABSORBERS',NMOL

    CALL INIT_PCRTM_ATM_ABS_STRUCT(  PCRTM_STND,&
                                     NLAY,      &
                                     NT,        &
                                     NMOL,      &
                                     NM   )

    CALL INIT_PCRTM_ATM_ABSORPTION(PCRTM_ATM_ABS_COEF, PCRTM_STND)
    
    ALLOCATE(   ABSCOEF(NM,NLAY,NT,NMOL+3),  &
                KFIX(NM,NLAY,NT),            &
                STAT = ALLOC_STAT   )   
    IF ( ALLOC_STAT /= 0 ) THEN
       PRINT*,'ERROR TRYING TO ALLOCATE VARIABLE TO READ IN ABSORPTION COEFFICIENTS'
       STOP
    ENDIF

    READ(IUABS) PCRTM_STND%FRQ(1:NM)

    READ(IUABS) ((((ABSCOEF(IM,I1,IT,IG),I1=NLAY,1,-1), &
                    IM=1,NM),IT=1,NT),IG=1,NMOL+3)
    READ(IUABS) ((PCRTM_STND%TSTD(I1,IT),I1=NLAY,1,-1),IT=1,NT)
    READ(IUABS) ((PCRTM_STND%FIXAMT(IT,I1),I1=NLAY,1,-1),IT=1,NT)
    READ(IUABS) (((PCRTM_STND%GASSTD(I1,IT,IG),I1=NLAY,1,-1),IT=1,NT),IG=1,NMOL)
    READ(IUABS) PCRTM_STND%PBND(1:NLAY+1)

    PCRTM_STND%NGAS=0
    ! 1ST DETERMINE WHICH OD NEED TO BE ADDED TO GAS:
    DO IM=1,NM
       DO IG=2,NMOL
          IF(PCRTM_STND%MOLINDX(IG).NE.0)THEN
             IF(ABSCOEF(IM,50,1,IG+3)*&
                PCRTM_STND%GASSTD(50,1,IG).GE.1.E-10) THEN
                PCRTM_STND%NGAS(IM)=PCRTM_STND%NGAS(IM)+1
                PCRTM_STND%IDGAS(PCRTM_STND%NGAS(IM),IM)=IG
             ENDIF
          ENDIF
       ENDDO
    ENDDO

    DO IT=1,NT
       DO I1=1,NLAY
          DO IM=1,NM
             KFIX(IM,I1,IT) = ABSCOEF(IM,I1,IT,1)*PCRTM_STND%FIXAMT(IT,I1)
             PCRTM_ATM_ABS_COEF%KH2O1(IM,I1,IT)=ABSCOEF(IM,I1,IT,2)
             PCRTM_ATM_ABS_COEF%KH2O2(IM,I1,IT)=ABSCOEF(IM,I1,IT,3)
             PCRTM_ATM_ABS_COEF%KH2O3(IM,I1,IT)=ABSCOEF(IM,I1,IT,4)
             DO IG=2,NMOL
                IF(PCRTM_STND%MOLINDX(IG).EQ.0)THEN
                   KFIX(IM,I1,IT)= KFIX(IM,I1,IT)+ &
                        ABSCOEF(IM,I1,IT,IG+3)*PCRTM_STND%GASSTD(I1,IT,IG)
                ELSE
                   DO IGG=1,PCRTM_STND%NGAS(IM)
                      IF(IG.EQ.PCRTM_STND%IDGAS(IGG,IM)) EXIT
                   ENDDO
                   IF(IGG.LE.PCRTM_STND%NGAS(IM))THEN
                      PCRTM_ATM_ABS_COEF%KGAS(IGG,IM,I1,IT)=ABSCOEF(IM,I1,IT,IG+3)
                   ELSE
                      KFIX(IM,I1,IT)=KFIX(IM,I1,IT)+ &
                           ABSCOEF(IM,I1,IT,IG+3)*PCRTM_STND%GASSTD(I1,IT,IG)
                   ENDIF
                ENDIF
             ENDDO
          ENDDO !DO IM
       ENDDO ! DO I1
    ENDDO   !DO IT
    
    ! NOW  DETERMINE WHICH FIXED GAS OD TO RETAIN:
    DO IM=1,NM
       IF (KFIX(IM,50,1).GE.1.E-10) THEN
          PCRTM_STND%NGAS(IM)=PCRTM_STND%NGAS(IM)+1
          PCRTM_STND%IDGAS(PCRTM_STND%NGAS(IM),IM)=NMOL+1
       ENDIF
    ENDDO    

    DO IT=1,NT
       DO I1=1,NLAY
          DO IM=1,NM
             IF(PCRTM_STND%IDGAS(PCRTM_STND%NGAS(IM),IM).EQ.NMOL+1)THEN
                PCRTM_ATM_ABS_COEF%KGAS(PCRTM_STND%NGAS(IM),IM,I1,IT)= KFIX(IM,I1,IT)
             ENDIF
          ENDDO !DO IM
       ENDDO ! DO I1
    ENDDO   !DO IT

    CLOSE(IUABS)

    DEALLOCATE( ABSCOEF, KFIX, STAT = DEALLOC_STAT )    
    IF ( DEALLOC_STAT /= 0 ) THEN
       PRINT*,'ERROR TRYING TO DEALLOCATE ABSCOEF OR KFIX'
       STOP
    ENDIF  
    

  END SUBROUTINE RD_PCRTM_ATM_ABSCOEF

END MODULE PCRTM_ATM_ABSORPTION_IO
