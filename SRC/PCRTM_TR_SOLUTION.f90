MODULE PCRTM_TR_SOLUTION

  USE PCRTM_MATH_UTILITY,     ONLY : PLANCK
  USE PCRTM_TYPE_KIND
  USE PCRTM_RT_SOLUTION_DEFINE
  USE PCRTM_PC_SOLUTION

  TYPE PCRTM_TR_SOLUTION_TYPE
     LOGICAL                  :: FLAG
     REAL(SINGLE),ALLOCATABLE :: TRLAY_CH(:,:)  
  END TYPE PCRTM_TR_SOLUTION_TYPE

CONTAINS

  SUBROUTINE INIT_PCRTM_TR_SOLUTION(TR_SOLUTION, NCH, NLAY)
    TYPE(PCRTM_TR_SOLUTION_TYPE),     INTENT(OUT) :: TR_SOLUTION
    INTEGER,                          INTENT(IN)  :: NCH
    INTEGER,                          INTENT(IN)  :: NLAY

    INTEGER                                       :: ALLOC_STAT

    TR_SOLUTION%FLAG = .TRUE.

    ALLOCATE(TR_SOLUTION%TRLAY_CH(NCH, NLAY),           &
             STAT = ALLOC_STAT)
    
    IF(ALLOC_STAT/=0) THEN
       PRINT*,'ERROR TRYING TO ALLOCATE TR_SOLUTION'
       STOP
    ENDIF
    
    TR_SOLUTION%TRLAY_CH = 1
   
  END SUBROUTINE INIT_PCRTM_TR_SOLUTION

  SUBROUTINE CLEAR_PCRTM_TR_SOLUTION(TR_SOLUTION)
    TYPE(PCRTM_TR_SOLUTION_TYPE), INTENT(INOUT) :: TR_SOLUTION
    INTEGER                                     :: DEALLOC_STAT

    IF(TR_SOLUTION%FLAG) THEN
  
       DEALLOCATE(TR_SOLUTION%TRLAY_CH,           &
                  STAT = DEALLOC_STAT)
       IF(DEALLOC_STAT/=0) THEN
          PRINT*,'ERROR TRYING TO DEALLOCATE TR_SOLUTION'
          STOP
       ENDIF
    
    END IF
       
  END SUBROUTINE CLEAR_PCRTM_TR_SOLUTION



  SUBROUTINE PCRTM_TR_SOLUTION_SOLVER(  TR_SOLUTION,        &
                                        RT_SOLUTION,        &
                                        EOF_SOLUTION,       &
                                        PCRTM_STND,         &
                                        NTOP,               &
                                        NBOT )
    TYPE(PCRTM_TR_SOLUTION_TYPE),     INTENT(INOUT) :: TR_SOLUTION
    TYPE(PCRTM_EOF_SOLUTION_TYPE),    INTENT(IN)    :: EOF_SOLUTION
    TYPE(PCRTM_RT_SOLUTION_TYPE),     INTENT(IN)    :: RT_SOLUTION
    TYPE(PCRTM_ATM_ABS_STRUCT_TYPE),  INTENT(IN)    :: PCRTM_STND
    INTEGER                                         :: NTOP, NBOT

    INTEGER                                         :: I1
    TYPE(PCRTM_RT_SOLUTION_TYPE)                    :: RT_SOLUTION_
    TYPE(PCRTM_EOF_SOLUTION_TYPE)                   :: EOF_SOLUTION_

    REAL(SINGLE)                                    :: TR(PCRTM_STND%NM)
    REAL(SINGLE), ALLOCATABLE                       :: RADCH1(:), RADCH2(:), RADCH_(:)
    REAL(SINGLE), ALLOCATABLE                       :: RADUP(:)
    INTEGER                                         :: ALLOC_STAT, DEALLOC_STAT

     TYPE(PCRTM_TR_SOLUTION_TYPE)                   :: TR_SOLUTION_

    ALLOCATE( RADCH1(EOF_SOLUTION%NCHBND),  &
              RADCH2(EOF_SOLUTION%NCHBND),  &
              RADCH_(EOF_SOLUTION%NCHBND),  &
              STAT = ALLOC_STAT )
    IF(ALLOC_STAT/=0) THEN
       PRINT*,'ERROR TRYING TO ALLOCATE RADCH IN PCRTM_TR_SOLUTION_SOLVER'
       STOP
    ENDIF    

    ALLOCATE( RADUP(PCRTM_STND%NM),         &
              STAT = ALLOC_STAT )
    IF(ALLOC_STAT/=0) THEN
       PRINT*,'ERROR TRYING TO ALLOCATE RADUP IN PCRTM_TR_SOLUTION_SOLVER'
       STOP
    ENDIF

    call INIT_PCRTM_TR_SOLUTION(TR_SOLUTION_, EOF_SOLUTION%NCHBND, PCRTM_STND%NLAY)


    CALL INIT_PCRTM_RT_SOLUTION( RT_SOLUTION_, PCRTM_STND )    

    CALL INIT_PCRTM_EOF_SOLUTION( EOF_SOLUTION_,           &
                                  EOF_SOLUTION%NREG,       &
                                  EOF_SOLUTION%NPCBND,     &
                                  EOF_SOLUTION%NCHBND )
    EOF_SOLUTION_ = EOF_SOLUTION
    EOF_SOLUTION_%BT_FLAG = .FALSE.

    RADUP = (1.0-RT_SOLUTION%EMIS)*RT_SOLUTION%RADDN/(RT_SOLUTION%TRTOP2LV(:,NBOT+1)+1E-20) &
         + RT_SOLUTION%EMIS*RT_SOLUTION%BS
 
    RT_SOLUTION_%RADUP = RADUP
    CALL PRED2PCSCORE(RT_SOLUTION_, EOF_SOLUTION_)
    CALL PCSCORE2CH(EOF_SOLUTION_)
    RADCH2 = EOF_SOLUTION_%RADCH

    DO I1 = NBOT, NTOP,-1

       TR = RT_SOLUTION%TRLAY(:,I1)

       RADCH1 = RADCH2

       RADUP = RADUP*TR + (1.0-TR)*RT_SOLUTION%BLAY(:,I1)
       RT_SOLUTION_%RADUP = RADUP
       CALL PRED2PCSCORE(RT_SOLUTION_, EOF_SOLUTION_)
       CALL PCSCORE2CH(EOF_SOLUTION_)
       RADCH2 = EOF_SOLUTION_%RADCH


       RT_SOLUTION_%RADUP = (1.0-TR)*RT_SOLUTION%BLAY(:,I1)
       CALL PRED2PCSCORE(RT_SOLUTION_, EOF_SOLUTION_)
       CALL PCSCORE2CH(EOF_SOLUTION_)
       RADCH_ = EOF_SOLUTION_%RADCH
       TR_SOLUTION_%TRLAY_CH(:,I1)  = (RADCH2-RADCH_)/(RADCH1)
       where(TR_SOLUTION_%TRLAY_CH(:,I1).gt.1) TR_SOLUTION_%TRLAY_CH(:,I1) = 1
       where(TR_SOLUTION_%TRLAY_CH(:,I1).lt.0) TR_SOLUTION_%TRLAY_CH(:,I1) = 0
       TR_SOLUTION%TRLAY_CH(:,I1) = TR_SOLUTION_%TRLAY_CH(:,I1)

!!$       RT_SOLUTION_%RADUP = RT_SOLUTION%Blay(:,I1)
!!$       CALL PRED2PCSCORE(RT_SOLUTION_, EOF_SOLUTION_)
!!$       CALL PCSCORE2CH(EOF_SOLUTION_)
!!$       RADCH_ = EOF_SOLUTION_%RADCH
!!$
!!$
!!$       TR_SOLUTION%TRLAY_CH(:,I1)  = (RADCH2-RADCH_)/(RADCH1-RADCH_)
!!$       where(abs(TR_SOLUTION%TRLAY_CH(:,I1)- TR_SOLUTION_%TRLAY_CH(:,I1)).ge.0.15)  &
!!$            TR_SOLUTION%TRLAY_CH(:,I1) = TR_SOLUTION_%TRLAY_CH(:,I1)
!!$       where(TR_SOLUTION%TRLAY_CH(:,I1).gt.1) TR_SOLUTION%TRLAY_CH(:,I1) = 1
!!$       where(TR_SOLUTION%TRLAY_CH(:,I1).lt.0) TR_SOLUTION%TRLAY_CH(:,I1) = 0       

    END DO

    call CLEAR_PCRTM_TR_SOLUTION(TR_SOLUTION_)

    CALL CLEAR_PCRTM_RT_SOLUTION( RT_SOLUTION_ )
    CALL CLEAR_PCRTM_EOF_SOLUTION(EOF_SOLUTION_)

    DEALLOCATE( RADCH1,  &
                RADCH2,  &
                RADCH_,  &
                STAT = DEALLOC_STAT )
    IF(DEALLOC_STAT/=0) THEN
       PRINT*,'ERROR TRYING TO DEALLOCATE RADCH IN PCRTM_TR_SOLUTION_SOLVER'
       STOP
    ENDIF    


    DEALLOCATE( RADUP, STAT = DEALLOC_STAT )
    IF(DEALLOC_STAT/=0) THEN
       PRINT*,'ERROR TRYING TO DEALLOCATE RADUP IN PCRTM_TR_SOLUTION_SOLVER'
       STOP
    ENDIF   


  END SUBROUTINE PCRTM_TR_SOLUTION_SOLVER


END MODULE PCRTM_TR_SOLUTION
