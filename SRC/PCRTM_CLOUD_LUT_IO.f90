MODULE PCRTM_CLOUD_LUT_IO

  USE PCRTM_FILE_UTILITIES
  USE PCRTM_TYPE_KIND
!##############################################################################


  INTEGER, PARAMETER :: TAUMAX=23,DMAX=18,THETMAX=9
  TYPE :: PCRTM_CLD_TABLE_DEF
     REAL :: D(DMAX)
     REAL :: TAU_V(TAUMAX+1)
     REAL :: THET(THETMAX)
     REAL(SINGLE),ALLOCATABLE :: R_TAB(:,:,:,:) 
     REAL(SINGLE),ALLOCATABLE :: T_TAB(:,:,:,:) 
  END TYPE PCRTM_CLD_TABLE_DEF

CONTAINS
  
  SUBROUTINE INIT_PCRTM_ICECLD_GRID(ICE_GRID,NM)
    TYPE(PCRTM_CLD_TABLE_DEF), INTENT(OUT):: ICE_GRID 
    INTEGER,                   INTENT(IN) :: NM
    INTEGER                               :: I,ALLOC_STAT
    
    ALLOCATE(ICE_GRID%R_TAB(DMAX,TAUMAX+1,THETMAX,NM),&
             ICE_GRID%T_TAB(DMAX,TAUMAX+1,THETMAX,NM),&
             STAT = ALLOC_STAT)
    
    IF (ALLOC_STAT /= 0) THEN
       PRINT*,'ERROR TRYING TO ALLOCATE ICE_GRID'
       STOP
    ENDIF

    ICE_GRID%D=(/10,20,30,40,50,60,70,80,90,100,110,120,130,140,150,160,170,180/)
    DO I=1,THETMAX
       ICE_GRID%THET(I)=10*(I-1)
    END DO
    ICE_GRID%TAU_V(1)=0.0
    ICE_GRID%TAU_V(2)=0.01
    DO I=3,22   
       ICE_GRID%TAU_V(I)=0.5+(I-3)*0.5
    END DO
    ICE_GRID%TAU_V(23)=11.0
    ICE_GRID%TAU_V(24)=20.0


  END SUBROUTINE INIT_PCRTM_ICECLD_GRID

  SUBROUTINE INIT_PCRTM_WATCLD_GRID(WAT_GRID,NM)
    TYPE(PCRTM_CLD_TABLE_DEF), INTENT(OUT):: WAT_GRID
    INTEGER,                   INTENT(IN) :: NM

    INTEGER                               :: I, ALLOC_STAT
    
    WAT_GRID%D=2*(/2,4,6,8,10,12,14,16,18,20,25,30,35,40,50,60,80,100/)
    DO I=1,THETMAX
       WAT_GRID%THET(I)=10*(I-1)
    END DO
    WAT_GRID%TAU_V(1)=0.0
    WAT_GRID%TAU_V(2)=0.01
    WAT_GRID%TAU_V(3)=0.5
    DO I=4,19
       WAT_GRID%TAU_V(I)=I-3
    END DO
    WAT_GRID%TAU_V(20)=18.0
    WAT_GRID%TAU_V(21)=20.0
    WAT_GRID%TAU_V(22)=30.0       
    WAT_GRID%TAU_V(23)=50.0
    WAT_GRID%TAU_V(24)=100.0

    ALLOCATE(WAT_GRID%R_TAB(DMAX,TAUMAX+1,THETMAX,NM),&
             WAT_GRID%T_TAB(DMAX,TAUMAX+1,THETMAX,NM),&
             STAT = ALLOC_STAT)
    
    IF (ALLOC_STAT /= 0) THEN
       PRINT*,'ERROR TRYING TO ALLOCATE WAT_GRID'
       STOP
    ENDIF

  END SUBROUTINE INIT_PCRTM_WATCLD_GRID

  
  SUBROUTINE CLEAR_PCRTM_CLD_GRID(GRID)
    TYPE(PCRTM_CLD_TABLE_DEF),INTENT(INOUT):: GRID
    INTEGER                                :: DEALLOC_STAT
    
    DEALLOCATE(GRID%R_TAB,&
               GRID%T_TAB,&
               STAT = DEALLOC_STAT)   
    
    IF (DEALLOC_STAT /= 0) THEN
       PRINT*,'ERROR TRYING TO DEALLOCATE CLOUD GRID'
       STOP
    ENDIF

  END SUBROUTINE CLEAR_PCRTM_CLD_GRID


  SUBROUTINE READ_PCRTM_CLD_TAB(CLD_TAB_FILE,GRID,NM)

    TYPE(PCRTM_CLD_TABLE_DEF),INTENT(INOUT):: GRID
    CHARACTER(160),           INTENT(IN)   :: CLD_TAB_FILE
    INTEGER,                  INTENT(IN)   :: NM

    INTEGER :: IUTAB
    INTEGER :: INDEX1,I,J,K
    INTEGER :: ALLOC_STAT, DEALLOC_STAT
    REAL(SINGLE), ALLOCATABLE :: RT_ALL(:,:,:,:)!(DMAX,TAUMAX,2*THETMAX,NM)

    ALLOCATE(RT_ALL(DMAX,TAUMAX,2*THETMAX,NM),STAT = ALLOC_STAT)
    IF ( ALLOC_STAT /= 0 ) THEN
       PRINT*,'ERROR TRYING TO ALLOCATE RT_ALL'
       STOP
    ENDIF
    

    CALL GETLUN(IUTAB)
    OPEN(IUTAB,FILE=CLD_TAB_FILE,ACCESS='DIRECT',RECL=NM*THETMAX*2*4, &
         FORM='UNFORMATTED',STATUS='OLD')
    INDEX1 = 0
    RT_ALL = 0 
    PRINT*, 'TAUMAX, DMAX, THETMAX, NM', TAUMAX, DMAX, THETMAX, NM
    DO I=1,TAUMAX
       DO K=1,DMAX
          INDEX1=INDEX1+1
          READ(IUTAB,REC=INDEX1) RT_ALL(K,I,:,:) 
       ENDDO
    ENDDO
    CLOSE(IUTAB)
    
    DO I = 1,TAUMAX
       DO K = 1,DMAX 
          DO J = 1, THETMAX
             GRID%R_TAB(K,I+1,J,1:NM)= RT_ALL(K,I,J,1:NM)
             GRID%T_TAB(K,I+1,J,1:NM)= RT_ALL(K,I,9+J,1:NM) 
          END DO
       END DO
    END DO
    
    GRID%R_TAB(:,1,:,:)=0
    GRID%T_TAB(:,1,:,:)=1

    DEALLOCATE(RT_ALL,STAT = DEALLOC_STAT)
    IF ( DEALLOC_STAT /= 0 ) THEN
       PRINT*,'ERROR TRYING TO DEALLOCATE RT_ALL'
       STOP
    ENDIF
    
        
  END SUBROUTINE READ_PCRTM_CLD_TAB
  
END MODULE PCRTM_CLOUD_LUT_IO
